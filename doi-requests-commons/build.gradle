repositories {
    // Repository for running Embedded DynamoDB locally.
    maven { url "https://s3.eu-central-1.amazonaws.com/dynamodb-local-frankfurt/release" }
}


dependencies {
    api group: 'com.github.BIBSYSDEV', name: 'nva-commons', version: project.nvaCommonsVersion
    api group: 'com.github.BIBSYSDEV', name: 'nva-datamodel-java', version: project.nvaDatamodelJavaVersion
    api group: 'io.mikael', name: 'urlbuilder', version: '2.0.9'
    api group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: project.jacksonVersion
    api group: 'com.fasterxml.jackson.core', name: 'jackson-annotations', version: project.jacksonVersion
    api group: 'com.fasterxml.jackson.datatype', name: 'jackson-datatype-jsr310', version: project.jacksonVersion
    api group: 'com.fasterxml.jackson.datatype', name: 'jackson-datatype-jdk8', version: project.jacksonVersion
    api group: 'org.zalando', name: 'problem', version: '0.23.0'
    api group: 'org.zalando', name: 'jackson-datatype-problem', version: '0.23.0'
    api group: 'com.amazonaws', name: 'aws-lambda-java-core', version: '1.2.0'
    api group: 'com.amazonaws', name: 'aws-java-sdk-lambda', version: project.awsSdkVersion
    api group: 'com.amazonaws', name: 'aws-java-sdk-dynamodb', version: project.awsSdkVersion
    api group: 'com.amazonaws', name: 'aws-java-sdk-sts', version:project.awsSdkVersion


    implementation(project(':doi-request-common-constants'))
    testImplementation('com.amazonaws:DynamoDBLocal:1.12.0') {
        exclude group: 'com.amazonaws', module: 'aws-java-sdk-dynamodb'
        exclude group: 'com.amazonaws', module: 'aws-java-sdk-core'
    }

    testImplementation(project(':test-module')) {
        because("it contains the library for local dynamoDb instance")
    }
}

configurations.testImplementation.canBeResolved = true

task copyNativeDeps(type: Copy) {
    from(configurations.testImplementation) {
        include "*.dylib"
        include "*.so"
        include "*.dll"
    }
    into 'build/dynamodb-local'
}

test.dependsOn copyNativeDeps
test.doFirst {
    systemProperty "java.library.path", 'build/dynamodb-local'
}